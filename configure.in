dnl ##
dnl ##  configure.in -- GNU autoconf configuration source
dnl ##

AC_PREREQ(2.0)dnl
AC_REVISION($Revision: 1.0$)dnl

AC_INIT(README)
AC_CONFIG_AUX_DIR(etc)
AC_PREFIX_DEFAULT(/usr/local)

AC_SET_MAKE
AC_PROG_INSTALL


AC_MSG_CHECKING([for Perl interpreter])
TMPFILE=/tmp/x
rm -f $TMPFILE
c=0
for dir in `echo $PATH | sed -e 's/:/ /g'`; do
    for perl in perl5 perl perl4; do
         if test -f "$dir/$perl"; then
			 perl="$dir/$perl"
			 version=`$perl -v | grep version | sed -e 's/.* version //' -e 's/ with.*//'`
			 versionnum="`echo $version | sed -e 's/\.//g' -e 's/_//g'`"
			 versionnum=`expr $versionnum - $c`
			 echo "$versionnum $version $perl" >>$TMPFILE
		 fi
	done
	c=`expr $c + 1`
done
perlvers="`cat $TMPFILE | sort -u | tail -1 | cut '-d ' -f2`"
perlprog="`cat $TMPFILE | sort -u | tail -1 | cut '-d ' -f3`"
rm -f $TMPFILE
path_perl=$perlprog
AC_MSG_RESULT([$perlprog v$perlvers])
AC_SUBST(path_perl)


AC_MSG_CHECKING([for Perl module Getopt::Long])
TMPFILE=/tmp/x
rm -f $TMPFILE
cat >$TMPFILE <<'EOT'
eval {
    require Getopt::Long;
    Getopt::Long->require_version('2.06');
};
if ($@) {
    print "failed";
} else {
    print "ok";
}
EOT
rc=`$path_perl $TMPFILE`; 
rm -f $TMPFILE
if test ".$rc" = .ok; then
	have_getoptlong=1
    AC_MSG_RESULT([found - will use installed version])
else
    have_getoptlong=0
    AC_MSG_RESULT([not found - will use private version])
fi
AC_SUBST(have_getoptlong)


AC_MSG_CHECKING([for Perl module Bit::Vector])
TMPFILE=/tmp/x
rm -f $TMPFILE
cat >$TMPFILE <<'EOT'
eval {
    require Bit::Vector;
    Bit::Vector->require_version('4.0');
};
if ($@) {
    print "failed";
} else {
    print "ok";
}
EOT
rc=`$path_perl $TMPFILE`; 
rm -f $TMPFILE
if test ".$rc" = .ok; then
	have_bitvector=1
    AC_MSG_RESULT([found - will use installed version])
else
    have_bitvector=0
    AC_MSG_RESULT([not found - will use private version])
fi
AC_SUBST(have_bitvector)


AC_MSG_CHECKING([for MakeMaker's private install paths])
TMPDIR=/tmp/ac
rm -rf $TMPDIR 2>/dev/null
mkdir $TMPDIR
cat >$TMPDIR/Makefile.PL <<'EOT'
use ExtUtils::MakeMaker;
WriteMakefile(
    'NAME'		=> 'Dummy::Dummy',
    'VERSION'	=> '0.0',
);
sub MY::postamble {
	'
dummy:
	@echo INSTALLPRIVLIB=$(INSTALLPRIVLIB)
	@echo INSTALLARCHLIB=$(INSTALLARCHLIB)
	@echo INSTALLSITELIB=$(INSTALLSITELIB)
	@echo INSTALLSITEARCH=$(INSTALLSITEARCH)
';
}
EOT
test "x$prefix" = xNONE && prefix=$ac_default_prefix
test "x$exec_prefix" = xNONE && exec_prefix='${prefix}'
eval "dir=$libdir"
( cd $TMPDIR; eval "$path_perl Makefile.PL PREFIX=$dir/perl_private >/dev/null 2>/dev/null" )
for line in `make -f $TMPDIR/Makefile dummy | grep '^INSTALL'`; do
	eval "$line"
done
rm -rf $TMPDIR 2>/dev/null
AC_MSG_RESULT([ok])
AC_SUBST(INSTALLPRIVLIB)
AC_SUBST(INSTALLARCHLIB)
AC_SUBST(INSTALLSITELIB)
AC_SUBST(INSTALLSITEARCH)

AC_OUTPUT(dnl
Makefile dnl
t/Makefile dnl
lib/Makefile dnl
slice_boot.pl dnl
etc/expand dnl
,dnl
chmod a+x etc/expand
)dnl

dnl ##EOF##
